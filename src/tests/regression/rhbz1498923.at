FWD_START_TEST([invalid direct rule causes reload error])
AT_KEYWORDS(direct reload rhbz1498923)

dnl Verify runtime interface to zone assignment is gone after reload
AT_CHECK([sed -i 's/^FlushAllOnReload.*/FlushAllOnReload=yes/' ./firewalld.conf])
FWD_RELOAD
FWD_CHECK([-q --zone=public --add-interface=foobar0])
FWD_CHECK([--get-zone-of-interface=foobar0], 0, [dnl
public
])
FWD_RELOAD
FWD_CHECK([--get-zone-of-interface=foobar0], 2, [], [dnl
no zone
])

dnl Below we test retention of some items applicable to FlushAllOnReload=no
AT_CHECK([sed -i 's/^FlushAllOnReload.*/FlushAllOnReload=no/' ./firewalld.conf])
FWD_RELOAD

FWD_CHECK([-q --permanent --direct --add-rule ipv4 filter INPUT 0 -p tcp --dport 8080 -j ACCEPT])
FWD_CHECK([-q --permanent --direct --add-rule ipv4 filter INPUT 1 --a-bogus-flag])

dnl add some non-permanent things that should persist a reload
FWD_CHECK([-q --zone=public --add-interface=foobar0])
FWD_CHECK([-q --direct --direct --add-rule ipv4 filter FORWARD 0 -p tcp -j ACCEPT])

FWD_RELOAD(13, [ignore], [ignore], 251)
FWD_CHECK([--state], 251, [ignore], [failed
])

dnl verify the non-permanent stuff we set above remained
FWD_CHECK([--get-zone-of-interface=foobar0], 0, [dnl
public
])
FWD_CHECK([-q --direct --direct --query-rule ipv4 filter FORWARD 0 -p tcp -j ACCEPT])

dnl now remove the bad rule and reload successfully
FWD_CHECK([-q --permanent --direct --remove-rule ipv4 filter INPUT 1 --a-bogus-flag])
FWD_RELOAD

dnl verify the non-permanent stuff we set above remained
FWD_CHECK([--get-zone-of-interface=foobar0], 0, [dnl
public
])
FWD_CHECK([-q --direct --direct --query-rule ipv4 filter FORWARD 0 -p tcp -j ACCEPT])
FWD_END_TEST([-e '/.*a-bogus-flag.*/d'])
