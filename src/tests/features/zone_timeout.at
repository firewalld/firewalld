m4_ifdef([TESTING_FIREWALL_OFFLINE_CMD], [], [
FWD_START_TEST([zone - timeouts])
AT_KEYWORDS(zone timeout)

FWD_CHECK([--permanent --zone trusted --add-icmp-block-inversion], 0, [ignore])
FWD_CHECK([--permanent --zone trusted --add-interface foobar], 0, [ignore])
FWD_CHECK([--permanent --zone trusted --remove-forward], 0, [ignore])
FWD_RELOAD()

dnl $1 = firewall-cmd args
dnl $2 = nftables expected input chain rules
dnl $3 = nftables expected forward chain rules
dnl $4 = nftables expected nat prerouting chain rules
dnl $5 = nftables expected nat postrouting chain rules
dnl $6 = iptables expected input chain rules
dnl $7 = iptables expected forward chain rules
dnl $8 = iptables expected nat prerouting chain rules
dnl $9 = iptables expected nat postrouting chain rules
m4_define([do_timeout], [dnl

FWD_CHECK([--zone trusted --timeout 2 $1], 0, [ignore])
FWD_CHECK([--zone trusted --add-service https], 0, [ignore])

NFT_LIST_RULES([inet], [filter_IN_trusted_allow], 0, [dnl
    table inet firewalld {
        chain filter_IN_trusted_allow {
            $2 dnl
            tcp dport 443 accept
        }
    }
])
NFT_LIST_RULES([inet], [filter_FWD_trusted_allow], 0, [dnl
    table inet firewalld {
        chain filter_FWD_trusted_allow {
            $3 dnl
        }
    }
])
NFT_LIST_RULES([inet], [nat_PRE_trusted_allow], 0, [dnl
    table inet firewalld {
        chain nat_PRE_trusted_allow {
            $4 dnl
        }
    }
])
NFT_LIST_RULES([inet], [nat_POST_trusted_allow], 0, [dnl
    table inet firewalld {
        chain nat_POST_trusted_allow {
            $5 dnl
        }
    }
])
IPTABLES_LIST_RULES([filter], [IN_trusted_allow], 0, [dnl
    $6 dnl
    ACCEPT 6 -- 0.0.0.0/0 0.0.0.0/0 tcp dpt:443
])
IPTABLES_LIST_RULES([filter], [FWD_trusted_allow], 0, [dnl
    $7 dnl
])
IPTABLES_LIST_RULES([nat], [PRE_trusted_allow], 0, [dnl
    $8 dnl
])
IPTABLES_LIST_RULES([nat], [POST_trusted_allow], 0, [dnl
    $9 dnl
])

sleep 5
NFT_LIST_RULES([inet], [filter_IN_trusted_allow], 0, [dnl
    table inet firewalld {
        chain filter_IN_trusted_allow {
            tcp dport 443 accept
        }
    }
])
NFT_LIST_RULES([inet], [filter_FWD_trusted_allow], 0, [dnl
    table inet firewalld {
        chain filter_FWD_trusted_allow {
        }
    }
])
NFT_LIST_RULES([inet], [nat_PRE_trusted_allow], 0, [dnl
    table inet firewalld {
        chain nat_PRE_trusted_allow {
        }
    }
])
NFT_LIST_RULES([inet], [nat_POST_trusted_allow], 0, [dnl
    table inet firewalld {
        chain nat_POST_trusted_allow {
        }
    }
])
IPTABLES_LIST_RULES([filter], [IN_trusted_allow], 0, [dnl
    ACCEPT 6 -- 0.0.0.0/0 0.0.0.0/0 tcp dpt:443
])
IPTABLES_LIST_RULES([filter], [FWD_trusted_allow], 0, [dnl
])
IPTABLES_LIST_RULES([nat], [PRE_trusted_allow], 0, [dnl
])
IPTABLES_LIST_RULES([nat], [POST_trusted_allow], 0, [dnl
])

FWD_CHECK([--zone trusted --remove-service https], 0, [ignore])

]) dnl end m4_define()

dnl ##################################
dnl ##### Test every option that #####
dnl ##### supports timeouts      #####
dnl ##################################

do_timeout([--add-port 1234/tcp],
    tcp dport 1234 accept
[dnl
], [], [], [],
[dnl
    ACCEPT 6 -- 0.0.0.0/0 0.0.0.0/0 tcp dpt:1234
], [], [], [])

do_timeout([--add-source-port 1234/tcp],
[dnl
    tcp sport 1234 accept
], [], [], [],
[dnl
    ACCEPT 6 -- 0.0.0.0/0 0.0.0.0/0 tcp spt:1234
], [], [], [])

do_timeout([--add-protocol igmp],
    meta l4proto igmp accept
[dnl
], [], [], [],
[dnl
    ACCEPT 2 -- 0.0.0.0/0 0.0.0.0/0
], [], [], [])

do_timeout([--add-service http],
    tcp dport 80 accept
[dnl
], [], [], [],
[dnl
    ACCEPT 6 -- 0.0.0.0/0 0.0.0.0/0 tcp dpt:80
], [], [], [])

do_timeout([--add-rich-rule 'rule service name=irc accept'],
[dnl
    tcp dport 6667 accept
], [], [], [],
[dnl
    ACCEPT 6 -- 0.0.0.0/0 0.0.0.0/0 tcp dpt:6667
], [], [], [])

do_timeout([--add-icmp-block echo-request],
[dnl
    icmp echo-request accept
    icmpv6 echo-request accept
], [], [], [],
[dnl
    ACCEPT 1 -- 0.0.0.0/0 0.0.0.0/0 icmptype 8
], [], [], [])

do_timeout([--add-forward],
[], [dnl
    oifname "foobar" accept
], [], [], [], [dnl
    ACCEPT 0 -- 0.0.0.0/0 0.0.0.0/0
], [], [])

do_timeout([--add-forward-port port=2222:proto=tcp:toport=22],
[], [], [dnl
    meta nfproto ipv4 tcp dport 2222 redirect to :22
], [], [], [], [dnl
    DNAT 6 -- 0.0.0.0/0 0.0.0.0/0 tcp dpt:2222 to::22
], [])

do_timeout([--add-masquerade],
[], [], [], [dnl
    meta nfproto ipv4 oifname != "lo" masquerade
], [], [], [], [dnl
    MASQUERADE 0 -- 0.0.0.0/0 0.0.0.0/0
])

m4_undefine([do_timeout])
FWD_END_TEST()
])
